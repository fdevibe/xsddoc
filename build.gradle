plugins {
    id 'java-library'
}

ant.importBuild('build.xml') { String oldTargetName ->
    switch (oldTargetName) {
        case 'javadoc': return 'ant_javadoc'
        case 'init': return 'ant_init'
        case 'jar': return 'ant_jar'
        default: return oldTargetName
    }
}

sourceSets {
    main {
        java {
            srcDirs = [ 'src' ]
            outputDir = file('classes')
        }
    }
    test {
        java {
            srcDirs = [ 'test' ]
        }
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.apache.ant:ant:1.10.6'
    implementation 'org.apache.axis2:axis2-adb:1.7.9'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    // testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'com.google.jimfs:jimfs:1.1'
}

// compileJava.dependsOn = [ 'prepare' ] // (1)
deploy.dependsOn = [ 'compileJava' ]  // (2)
assemble.dependsOn = [ 'package' ]  // (3)